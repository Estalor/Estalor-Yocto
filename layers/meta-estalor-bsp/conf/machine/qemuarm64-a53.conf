#@TYPE: Machine
#@NAME: QEMU ARMv8 machine with A53 tuning
#@DESCRIPTION: Machine configuration for running an ARMv8 system on QEMU

MACHINEOVERRIDES = "qemuarm64:${MACHINE}"

# Machine settings
#########################################
require conf/machine/include/arm/armv8a/tune-cortexa53.inc
require conf/machine/include/qemu.inc

KERNEL_IMAGETYPE = "Image"
KMACHINE = "qemuarm64"

MACHINE_FEATURES:append = " \
    vfat \
    usbgadget \
    usbhost \
"

IMAGE_FSTYPES:append = " \
    wic \
    ext4 \
"

EXTRA_IMAGEDEPENDS:append = " u-boot"

# WIC settings
#########################################
WKS_FILES = "qemuarm64.wks"

IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE} \
    u-boot.bin \
    boot.scr \
"

do_image_wic:append[depends] = " \
    virtual/bootloader:do_deploy \
"

# Bootloader settings
#########################################
UBOOT_MACHINE = "qemu_arm64_defconfig"
# Ensure that U-Boot is using the boot script instead of extern Linux Kernel directly
UBOOT_EXTLINUX = "0"
KERNEL_BOOTCMD = "booti"

# Debug settings
#########################################
SERIAL_CONSOLES ?= "115200;ttyAMA0 115200;hvc0"
SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"

# Qemu Settings
#########################################
QB_SYSTEM_NAME = "qemu-system-aarch64"
QB_MACHINE = "-machine virt"
QB_CPU = "-cpu cortex-a53"
QB_SMP ?= "-smp 4"
QB_CPU_KVM = "-cpu host -machine gic-version=3"
# For graphics to work we need to define the VGA device as well as the necessary USB devices
QB_GRAPHICS = "-device virtio-gpu-pci"
QB_OPT_APPEND = "-device qemu-xhci -device usb-tablet -device usb-kbd"
# Virtio Networking support
QB_TAP_OPT = "-netdev tap,id=net0,ifname=@TAP@,script=no,downscript=no"
QB_NETWORK_DEVICE = "-device virtio-net-pci,netdev=net0,mac=@MAC@"
# Virtio block device
QB_ROOTFS_OPT = "-drive id=disk0,file=@ROOTFS@,if=none,format=raw -device virtio-blk-pci,drive=disk0"
# Virtio serial console
QB_SERIAL_OPT = "-device virtio-serial-pci -chardev null,id=virtcon -device virtconsole,chardev=virtcon"
QB_TCPSERIAL_OPT = "-device virtio-serial-pci -chardev socket,id=virtcon,port=@PORT@,host=127.0.0.1 -device virtconsole,chardev=virtcon"
# Bootloader
QB_DEFAULT_BIOS = "u-boot.bin"
QB_FSINFO = "wic:no-kernel-in-fs"
QB_KERNEL_ROOT = "/dev/vda3"
QB_DRIVE_TYPE = "/dev/vd"
QB_DEFAULT_FSTYPE = "ext4"
# Port forwarding settings
QB_SLIRP_OPT="-netdev user,id=net0,hostfwd=tcp::3218-:8123,hostfwd=tcp::2222-:22"