#@TYPE: Machine
#@NAME: RaspberryPi 4 Compute Module in the reterminal
#@DESCRIPTION: Machine configuration for the RaspberryPi 4 in 64 bits mode

MACHINEOVERRIDES = "raspberrypi4:raspberrypi4-64:${MACHINE}"

# Machine settings
#########################################
require conf/machine/raspberrypi4-64.conf

# We want the UART so that we can easily use a serial terminal while debugging
ENABLE_UART = "1"

# Since several devices are connected to the I2C bus (such as the screen) we will need I2C
ENABLE_I2C = "1"
KERNEL_MODULE_AUTOLOAD:append = " i2c-dev"
KERNEL_MODULE_AUTOLOAD:append = " als_ltr30x"
KERNEL_MODULE_AUTOLOAD:append = " lis331dlh-i2c"

IMAGE_FSTYPES = " \
    wic.bz2 \
    wic.bmap \
    ext4 \
"

MACHINE_FEATURES:append = " \
    usbhost \
    touchscreen \
    wifi \
    rtc \
    pci \
"

# Add additional dtbo files to the devicetree
RPI_KERNEL_DEVICETREE_OVERLAYS:append = "\
    overlays/i2c3.dtbo \
    overlays/vc4-kms-v3d-pi4.dtbo \
    overlays/reterminal.dtbo \
"

# Enable Reterminal hardware
ENABLE_RETERMINAL = "1"

# WIC settings
#########################################
WKS_FILES = "reterminal.wks"

# Bootloader settings
#########################################
# Default for the distro to U-Boot
RPI_USE_U_BOOT = "1"